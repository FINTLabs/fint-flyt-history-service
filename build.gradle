plugins {
    id 'org.springframework.boot' version '2.7.18'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'java'
    id 'com.github.ben-manes.versions' version '0.52.0'
}

group = 'no.fintlabs'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

jar {
    enabled = false
}

test {
    useJUnitPlatform {
        excludeTags "performance"
    }
}

def performanceTest = tasks.register("performanceTest", Test) {
    useJUnitPlatform {
        includeTags "performance"
    }
}

repositories {
    mavenCentral()
    maven {
        url "https://repo.fintlabs.no/releases"
    }
    mavenLocal()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'io.netty:netty-resolver-dns-native-macos:4.2.6.Final:osx-aarch_64'

    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'no.fintlabs:fint-flyt-resource-server:5.1.1'

    implementation 'io.hypersistence:hypersistence-utils-hibernate-55:3.9.5'

    implementation 'javax.validation:validation-api'
    implementation 'org.hibernate.validator:hibernate-validator'

    implementation 'org.springframework.kafka:spring-kafka'
    implementation 'no.fintlabs:fint-kafka:4.0.3'
    implementation 'no.fintlabs:fint-flyt-kafka:3.1.2'

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.postgresql:postgresql:42.7.7'

    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'

    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'cglib:cglib-nodep:3.3.0'
    testImplementation(platform("org.testcontainers:testcontainers-bom:1.21.3"))
    testImplementation("org.testcontainers:junit-jupiter")
    testImplementation("org.testcontainers:postgresql")
}

